# Run the project
run:
    cargo run

# Run with auto-reload
dev:
    fd -e rs | entr -rn cargo run

# Run tests
test:
    cargo test

# Run tests continuously (on file change)
test-watch:
    fd -e rs | entr -cn cargo test

# Add a dependency
add *packages:
    cargo add {{packages}}

# Open in browser (Wayland) or copy URL to clipboard
browse:
    #!/bin/sh
    u="http://${DEV_HOST:-localhost}:${PORT:-4000}"
    printf '\e]52;c;%s\a' "$(printf '%s' "$u" | base64 | tr -d '\n')" > /dev/tty
    if [ -n "$WAYLAND_DISPLAY" ]; then
        chromium --ozone-platform=wayland "$u" >/dev/null 2>&1 &
        echo "Opened browser: $u (copied to clipboard)"
    else
        echo "Copied to clipboard: $u"
    fi
